apply plugin: 'groovy'
apply plugin:'java-library'
apply from: 'databaseSetup.gradle'

repositories {
    mavenCentral()
}

dependencies {
    api project(':model')
    testImplementation 'junit:junit:4.11'
    testImplementation 'org.testng:testng:6.8'
    testImplementation 'org.codehaus.groovy:groovy:2.0.6'
    testImplementation 'org.spockframework:spock-core:0.7-groovy-2.0'
    testImplementation 'com.h2database:h2:1.3.170'
}

test {
    reports.html.destination = file("$reports.html.destination/unit")
    reports.junitXml.destination = file("$reports.junitXml.destination/unit")
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testCompileClasspath
        runtimeClasspath = output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    reports.html.destination = file("$reports.html.destination/integration")
    reports.junitXml.destination = file("$reports.junitXml.destination/integration")
    dependsOn compileIntegrationTestJava, startAndPrepareDatabase
    finalizedBy stopDatabase
}

tasks.withType(Test) {
    testLogging {
        showStandardStreams = true
        exceptionFormat 'full'
    }
}

check.dependsOn integrationTest